#! winxed
// Test file for winxedxx supported features
// All features used here should at least compile

//**********************************************************************

// Minimalistic TAP functions

class Test
{
    var count;
    function Test()
    {
        self.count = 0;
    }
    function inc_count()
    {
        int count = self.count;
        ++count;
        self.count =: count;
        return count;
    }
    function ok(int check, string message)
    {
        if (! check)
            print("not ");
        print("ok ");
        print(self.inc_count());
        say(" - ", message);
    }

    function is(int value, int check, string message)
    {
        if (value != check)
            print("not ");
        print("ok ");
        print(self.inc_count());
        say(" - ", message);
    }

    function is_string(string value, string check, string message)
    {
        if (value != check)
            print("not ");
        print("ok ");
        print(self.inc_count());
        say(" - ", message);
    }
    function done_testing()
    {
        say("1..", self.count);
    }
}

//**********************************************************************

function array(test)
{
    var arr = [ "a", "b", "c", "d" ];
    test.is(elements(arr), 4, "array literal");
    test.is_string(arr[2], "c", "array element initialized");
}

namespace Foo
{

function bar()
{
    return "Foo.bar";
}

} // namespace Foo

function bar()
{
    return "bar";
}

class SomeClass
{
}

function test_switchcase(test)
{
    int i = 42;
    int result = 0;
    switch {
      case i == 42:
        result = 1;
        break;
      default:
        // Make sure the previous break skip this
        result = 0;
    }
    test.is(result, 1, "switchcase");
}

//**********************************************************************

function main [main] (args)
{
    var test = new Test();

    test.ok(1, "compiled and started");

    // No debug mode yet, just ensure this can be compiled
    __ASSERT__(1 == 1);

    int i = 1;
    test.ok(i == 1, "int initialization");

    i = -i;
    test.is(i, -1, "unary minus");

    i = 1;

    test.is(~i, -2, "bitwise not");

    test.is(i | 2, 3, "bitwise or");

    test.is(i ^ 3, 2, "bitwise xor");

    test.is(i << 2, 4, "shift left");
    i = 8;
    test.is(i >> 2, 2, "shift right");

    string s = "abc";
    test.is_string(s[1], "b", "string indexing");

    int j = 12;
    for (i = 0; i < 10; ++i)
        --j;
    test.is(j, 2, "for loop, inc and dec operators");

    j = 0;
    for (i in [ 1, 3, 5, 7 ])
        j += i;
    test.is(j, 16, "for in");

    array(test);

    test.is_string(Foo.bar(), "Foo.bar", "call function in namespace");
    test.is_string(bar(), "bar", "call function out of namespace with same name");

    test_switchcase(test);

    var obj = new SomeClass;
    test.ok(obj instanceof SomeClass, "new and instanceof by identifier");

    test.done_testing();
}

// End
